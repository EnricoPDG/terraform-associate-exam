## Input Variables
Input variables let you customize aspects of Terraform modules without altering the module's own source code. This allows you to share modules across different Terraform configurations, making your module composable and reusable.
- Input variables are like function arguments.
- [Output values](https://developer.hashicorp.com/terraform/language/v1.1.x/values/outputs) are like function return values.
- [Local values](https://developer.hashicorp.com/terraform/language/v1.1.x/values/locals) are like a function's temporary local variables.

### Declaring an Input Variable
```hcl
variable "image_id" {
  type = string
}

variable "availability_zone_names" {
  type    = list(string)
  default = ["us-west-1a"]
}

variable "docker_ports" {
  type = list(object({
    internal = number
    external = number
    protocol = string
  }))
  default = [
    {
      internal = 8300
      external = 8300
      protocol = "tcp"
    }
  ]
}

```

### Custom validation rules
```hcl
variable "image_id" {
  type        = string
  description = "The id of the machine image (AMI) to use for the server."

  validation {
    condition     = length(var.image_id) > 4 && substr(var.image_id, 0, 4) == "ami-"
    error_message = "The image_id value must be a valid AMI id, starting with \"ami-\"."
  }
}
```

### Assigning values to root module variables
Can assign values to a variables in a number of ways:
- In Terraform Cloud workspace
- Individually, with `-var` command line option.
- In variable definitions (`.tfvars`) files.
- As environment variables.

#### Variables on the command line
example:
```hcl
terraform apply -var="image_id=ami-abc123"
terraform apply -var='image_id_list=["ami-abc123","ami-def456"]' -var="instance_type=t2.micro"
terraform apply -var='image_id_map={"us-east-1":"ami-abc123","us-east-2":"ami-def456"}'
```

#### Variables Definitions (`.tfvars`) Files
To set lots of variables, it is more convenient to specify their values in a _variable definitions file_ (with a filename ending in either `.tfvars` or `.tfvars.json`) and then specify that file on the command line with `-var-file`:
```hcl
terraform apply -var-file="testing.tfvars"
```
.tfvars file:
```hcl
image_id = "ami-abc123"
availability_zone_names = [
  "us-east-1a",
  "us-west-1c",
]
```
Terraform also automatically loads a number of variable definitions files if they are present:
- Files named exactly `terraform.tfvars` or `terraform.tfvars.json`.
- Any files with names ending in `.auto.tfvars` or `.auto.tfvars.json`.
Files whose names end with `.json` are parsed instead as JSON objects:
```json
{
  "image_id": "ami-abc123",
  "availability_zone_names": ["us-west-1a", "us-west-1c"]
}
```

#### Environment Variables
Terraform searches the environment of its own process for environment variables named `TF_VAR_` followed by the name of a declared variable.
```hcl
export TF_VAR_image_id=ami-abc123
terraform plan
```

## Output Values
Output values make information about your infrastructure available on the command line, and can expose information for other Terraform configurations to use. Output values are similar to return values in programming languages.

Output values have several uses:
- A child module can use outputs to expose a subset of its resource attributes to a parent module.
- A root module can use outputs to print certain values in the CLI output after running `terraform apply`.
- When using [remote state](https://developer.hashicorp.com/terraform/language/v1.1.x/state/remote), root module outputs can be accessed by other configurations via a [`terraform_remote_state` data source](https://developer.hashicorp.com/terraform/language/v1.1.x/state/remote-state-data).

## Declaring an Output Value
```hcl
output "instance_ip_addr" {
  value = aws_instance.server.private_ip
}
```

## Accessing Child Module Outputs
In a parent module, outputs of child modules are available in expressions as `module.<MODULE NAME>.<OUTPUT NAME>`. For example, if a child module named `web_server` declared an output named `instance_ip_addr`, you could access that value as `module.web_server.instance_ip_addr`.

## Optional Arguments
### `description` - outputs value documentation

```hcl
output "instance_ip_addr" {
  value       = aws_instance.server.private_ip
  description = "The private IP address of the main server instance."
}
```

### `sensitive`- Suppressing values in CLI Output

```hcl
output "db_password" {
  value       = aws_db_instance.db.password
  description = "The password for logging in to the database."
  sensitive   = true
}
```

### `depends_on`- Explicit Output dependencies

```hcl
output "instance_ip_addr" {
  value       = aws_instance.server.private_ip
  description = "The private IP address of the main server instance."

  depends_on = [
    # Security group rule must be created before this IP address could
    # actually be used, otherwise the services will be unreachable.
    aws_security_group_rule.local_access,
  ]
}
```


