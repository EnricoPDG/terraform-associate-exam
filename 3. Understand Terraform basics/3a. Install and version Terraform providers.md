# Providers
- Providers allow Terraform to interact with cloud providers, SaaS providers, and other APIs.
- Some providers require to configure with endpoints URLs, cloud regions or other settings before Terraform can use them.
- All Terraform configuration must declare which providers they require so that Terraform can install and use them.

## Provider Configuration
- Provider configuration belong in the root module of a Terraform configuration.
- Provider configuration is created using a `provider`:  block:
```hcl
provider "google" {
	project = "test"
	region = "us-central"
}
```
- The name given in the block header "google" is the local name of the provider to configure, this provider should already be included in a `required_providers` block.
- The body of the block (between `{` and `}`) contains configuration **arguments** for the provider.
- Can use expression in the values of configuration arguments, but can only reference values that are knows before the configuration is applied.
- Some providers can use shell environments variables as values for some of their arguments.
- There are two "meta-arguments" that are defined by Terraform itself ad available for all `provider` blocks:
	- `alias`: for using the same provider with different configurations for different resources
	- `version`: which is no longer recommend

### `alias`: Multiple Provider Configuration
- You can optionally define multiple configurations for the same provider, and select which one to use on a per-resource or per-module basis.
- To create multiple configurations for a given provider, include multiple `provider` blocks with the same provider name. For each additional non-default configuration, use the `alias` meta-argument to provide an extra name segment. For example:
```hcl
# The default provider configuration; resources that begin with `aws_` will use
# it as the default, and it can be referenced as `aws`.
provider "aws" {
  region = "us-east-1"
}

# Additional provider configuration for west coast region; resources can
# reference this as `aws.west`.
provider "aws" {
  alias  = "west"
  region = "us-west-2"
}
```
- To declare a configuration alias within a module in order to receive an alternate provider configuration from the parent module, add the `configuration_aliases` argument to that provider's `required_providers` entry. The following example declares both the `mycloud` and `mycloud.alternate` provider configuration names within the containing module:
```hcl
terraform {
  required_providers {
    mycloud = {
      source  = "mycorp/mycloud"
      version = "~> 1.0"
      configuration_aliases = [ mycloud.alternate ]
    }
  }
}
```

#### Default Provider Configuration 
- A `provider` block without an `alias` argument is the default configuration for that provider.
- If every explicit configuration of a provider has a alias, Terraform uses the implied empty configuration as that provider's default configuration.
#### Referring to Alternate Provider Configuration
- It expects a reference of the **PROVIDER_NAME.ALIAS**, like `aws.west` would refer to the provider with the `us-west-2` region.
- These references are special expressions. Like reference to other named entities, they are not strings and don't need to be quoted.
- They are only valid in specific meta-arguments of resource, data and module blocks, and can't be used in arbitrary expressions.

#### Selecting Alternate Provider Configurations
- By default resources use default provider configuration (one without an alias argument)
- To use alternate provider configuration for a resource or data source, set its `provider` meta-argument to a **PROVIDER_NAME.ALIAS** reference:
```hcl
resource "aws_instance" "foo" {
  provider = aws.west

  # ...
}
```
- To select alternate provider configuration for a child module, use its `providers` meta-argument to specify which provider configurations should be mapped to which local provider names inside the module:
```hcl
module "aws_vpc" {
  source = "./aws_vpc"
  providers = {
    aws = aws.west
  }
}

```

### `version` (Deprecated)
The `version` meta-argument specifies a version constraint for a provider, and works the same way as the `version` argument in a [`required_providers` block](https://developer.hashicorp.com/terraform/language/v1.1.x/providers/requirements). The version constraint in a provider configuration is only used if `required_providers` does not include one for that provider.

**The `version` argument in provider configurations is deprecated.** In Terraform 0.13 and later, always declare provider version constraints in [the `required_providers` block](https://developer.hashicorp.com/terraform/language/v1.1.x/providers/requirements). The `version` argument will be removed in a future version of Terraform.

# Specifying Provider Requirements
The `required_providers` block specifies all of the providers required by the current module.
example:
```hcl
terraform {
  required_providers {
    aws = {
      version = ">= 2.7.0"
      source = "hashicorp/aws"
    }
  }
}
```

# Dependency Lock File
A Terraform configuration may refer to two different kinds of external dependency:
- Providers
- Modules

Both of these dependency types can be published and updated independently from Terraform itself. For that reason, Terraform must determine which versions of those dependencies are potentially compatible.

Dependency lock file tracks only providers dependencies. Terraform does not remember version selections for remote modules, and so Terraform will always select the newest available. You can use an _exact_ version constraint to ensure that Terraform will always select the same module version.

## Lock File Location
The dependency lock file is a file that belongs to the configuration as a whole.
The lock file is always named `.terraform.lock.hcl`.
Terraform automatically creates or updates the dependency lock file each time you run the `terraform init` command. You should include this file in your version control repository so that you can discuss potential changes to your external dependencies via code review.

ref: https://developer.hashicorp.com/terraform/language/files/dependency-lock

