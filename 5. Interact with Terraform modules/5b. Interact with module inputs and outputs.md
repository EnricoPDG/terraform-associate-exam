## Input Variables
Input variables let you customiza aspects of Terraform modules without altering the module's own source code. This functionality allows you to share modules across different Terraform configurations, making your module composable and reusable.

When you declare variables in the root module of your configuration, you can set their values using CLI options and environment variables. When you declare them in [child modules](https://developer.hashicorp.com/terraform/language/modules), the calling module should pass values in the `module` block.

If you're familiar with traditional programming languages, it can be useful to compare Terraform modules to function definitions:

- Input variables are like function arguments.
- [Output values](https://developer.hashicorp.com/terraform/language/values/outputs) are like function return values.
- [Local values](https://developer.hashicorp.com/terraform/language/values/locals) are like a function's temporary local variables.

## Declaring an Input Variable
```hcl
variable "image_id" {
  type = string
}

variable "availability_zone_names" {
  type    = list(string)
  default = ["us-west-1a"]
}

variable "docker_ports" {
  type = list(object({
    internal = number
    external = number
    protocol = string
  }))
  default = [
    {
      internal = 8300
      external = 8300
      protocol = "tcp"
    }
  ]
}
```

The name of a variable can be any valid [identifier](https://developer.hashicorp.com/terraform/language/syntax/configuration#identifiers) _except_ the following: `source`, `version`, `providers`, `count`, `for_each`, `lifecycle`, `depends_on`, `locals`.

## Arguments
Terraform CLI defines the following optional arguments for variable declarations:
- `default`
- `type` 
- `description`
- `validation` - a block do define validation rules, usually in addition to type constraints
- `sensitive` - Limits Terraform UI output when the variable is used in configuration
- `nullable` - Specify if the variable can be`null` within the module.

## Type Constraints
The `type` argument in a `variable` block allows to restrict the type of value that will be accepts.
While type constraints is optional it is recommended to use.

Type constraints are created from a mixture of type keywords and type constructors. The supported type keywords are:
- `string`
- `number`
- `bool`

The type constructors allow you to specify complex types such as collections:
- `list(<TYPE>)`
- `set(<TYPE>)`
- `map(<TYPE>)
- [`object({<ATTR NAME> = <TYPE>, ... })`](https://developer.hashicorp.com/terraform/language/values/variables#object)
- `tuple([<TYPE>, ...])`

The keyword `any` may be used to indicate that any type is acceptable.

## Custom validation Rules
Can specify a custom validation rules for a particular variable by adding a `validation` block within the corresponding `variable` block. The example below checks wheter the AMI ID has the correct syntax.

```hcl
variable "image_id" {
  type        = string
  description = "The id of the machine image (AMI) to use for the server."

  validation {
    condition     = length(var.image_id) > 4 && substr(var.image_id, 0, 4) == "ami-"
    error_message = "The image_id value must be a valid AMI id, starting with \"ami-\"."
  }
}
```

## Suppressing Values in CLI Output
Setting a variable as `sensitive` prevents Terraform from showing its value in the `plan` or `apply` output, when you use that variable elsewhere in your configuration.

Terraform will still record sensitive values in the [state](https://developer.hashicorp.com/terraform/language/state), and so anyone who can access the state data will have access to the sensitive values in cleartext. For more information, see [_Sensitive Data in State_](https://developer.hashicorp.com/terraform/language/state/sensitive-data).

```hcl
variable "user_information" {
  type = object({
    name    = string
    address = string
  })
  sensitive = true
}

resource "some_resource" "a" {
  name    = var.user_information.name
  address = var.user_information.address
}
```

## Disallowing Null Input Values
The `nullable` argument in a variable block controls whether the module caller may assign the value `null` to the variable.
```hcl
variable "example" {
  type     = string
  nullable = false
}
```

## Assigning Values to Root Module Variables
When variables are declared in the root module of your configuration, they can be set in a number of ways:
- In an HCP Terraform workspace.
- Individually, with `-var` command line option.
- In variable definitions (`.tfvars`) files, either specified on the command line or automatically loaded.
- As environment variables.

The following sections describe these options in more detail. This section does not apply to _child_ modules, where values for input variables are instead assigned in the configuration of their parent module, as described in [_Modules_](https://developer.hashicorp.com/terraform/language/modules).

### Variables on the Command Line
To specify individual variables on the command line, use the `var` option when running the `terraform plan` and `terraform apply` commands:
```shell
terraform apply -var="image_id=ami-abc123"
terraform apply -var='image_id_list=["ami-123", "ami-1234"]' -var="instance_type=t2.micro"
terraform apply -var='image_id_map={"us-east-1":"ami-123",{"sa-east-1":"ami-123"}'
```

### Variables Definitions (`.tfvars`) Files
To set lots of variables, it is more convenient to specify their values in a *variable definitions file* (with a filename ending in either `tfvar` or `.tfvars.json`) and then specify that file on the command line with `-var-file`: 
Linux, Mac OS, and UNIX:

```
terraform apply -var-file="testing.tfvars"
```

PowerShell:

```
terraform apply -var-file='testing.tfvars'
```

Windows `cmd.exe`:

```
terraform apply -var-file="testing.tfvars"
```

Terraform also automatically loads a number of variables definitions files if they are present:
- Files named exactly `terraform.tfvars` of `terraform.tfvars.json`.
- Any files with names ending in `auto.tfvars` or `.auto.tfvars.json`.

Files whose names end with `.json` are parsed instead as JSON objects, with the root object properties corresponding to variable names:

```
{
  "image_id": "ami-abc123",
  "availability_zone_names": ["us-west-1a", "us-west-1c"]
}
```

### Environment Variables
As a fallback for the other ways of defining variables, Terraform searches the environment of its own process for environment variables named `TF_VAR_` followed by the name of a declared variable.

This can be useful when running Terraform in automation, or when running a sequence of Terraform commands in successions with the same variables. For example, at a `bash` prompt or a Unix system:

```shell
$ export TF_VAR_image_id=ami-abc123
$ terraform plan
...
```

On operating systems where environment variable names are case-sensitive, Terraform matches the variable name exactly as given in configuration, and so the required environment variable name will usually have a mix of upper and lower case letters as in the above example.

### Complex-typed Values

When variable values are provided in a variable definitions file, you can use Terraform's usual syntax for [literal expressions](https://developer.hashicorp.com/terraform/language/expressions/types#literal-expressions) to assign complex-typed values, like lists and maps.

Some special rules apply to the `-var` command line option and to environment variables. For convenience, Terraform defaults to interpreting `-var` and environment variable values as literal strings, which need only shell quoting, and no special quoting for Terraform. For example, in a Unix-style shell:

```
$ export TF_VAR_image_id='ami-abc123'
```

However, if a root module variable uses a [type constraint](https://developer.hashicorp.com/terraform/language/values/variables#type-constraints) to require a complex value (list, set, map, object, or tuple), Terraform will instead attempt to parse its value using the same syntax used within variable definitions files, which requires careful attention to the string escaping rules in your shell:

```
$ export TF_VAR_availability_zone_names='["us-west-1b","us-west-1d"]'
```

For readability, and to avoid the need to worry about shell escaping, we recommend always setting complex variable values via variable definitions files. For more information on quoting and escaping for `-var` arguments, see [Input Variables on the Command Line](https://developer.hashicorp.com/terraform/cli/commands/plan#input-variables-on-the-command-line).

### Variable Definition Precedence

The above mechanisms for setting variables can be used together in any combination. If the same variable is assigned multiple values, Terraform uses the _last_ value it finds, overriding any previous values. Note that the same variable cannot be assigned multiple values within a single source.

Terraform loads variables in the following order, with later sources taking precedence over earlier ones:

- Environment variables
- The `terraform.tfvars` file, if present.
- The `terraform.tfvars.json` file, if present.
- Any `*.auto.tfvars` or `*.auto.tfvars.json` files, processed in lexical order of their filenames.
- Any `-var` and `-var-file` options on the command line, in the order they are provided. (This includes variables set by an HCP Terraform workspace.)

## Accessing Module Output Values

The resources defined in a module are encapsulated, so the calling module cannot access their attributes directly. However, the child module can declare [output values](https://developer.hashicorp.com/terraform/language/v1.1.x/values/outputs) to selectively export certain values to be accessed by the calling module.

For example, if the `./app-cluster` module referenced in the example above exported an output value named `instance_ids` then the calling module can reference that result using the expression `module.servers.instance_ids`:

```
resource "aws_elb" "example" {
  # ...

  instances = module.servers.instance_ids
}
```
